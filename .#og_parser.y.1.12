%{
//-- don't change *any* of these: if you do, you'll break the compiler.
#include <cdk/compiler.h>
#include "ast/all.h"
#define LINE               compiler->scanner()->lineno()
#define yylex()            compiler->scanner()->scan()
#define yyerror(s)         compiler->scanner()->error(s)
#define YYPARSE_PARAM_TYPE std::shared_ptr<cdk::compiler>
#define YYPARSE_PARAM      compiler
//-- don't change *any* of these --- END!
%}

%union {
  int                   i;	/* integer value */
  std::string          *s;	/* symbol name or string literal */
  double                d;    /* real value */
  cdk::basic_node      *node;	/* node pointer */
  cdk::sequence_node   *sequence;
  cdk::expression_node *expression; /* expression nodes */
  cdk::basic_type      *t;       /* expression type */  
  cdk::lvalue_node     *lvalue;
  og::block_node       *b;
};

%token<d> tREALVAL
%token<expression> tNULLPTR;
%token <i> tINTEGER tPUBLIC tREQUIRE tPRIVATE
%token <s> tIDENTIFIER tSTRING

%token tINT tREAL tSTR tAUTO tPTR
%token tPROCEDURE
%token tSIZEOF
%token tINPUT tWRITE tWRITELN
%token tBREAK tCONTINUE tRETURN
%token tIF tTHEN tELSE tELIF
%token tFOR tDO
%token tGE tLE tEQ tNE tOR tAND

%nonassoc tIF 
%nonassoc tTHEN tDO
%nonassoc tELSE tELIF

%left tOR 
%left tAND
%left '~'
%right '='
%left tEQ tNE
%left tGE tLE '>' '<'
%left '+' '-' 
%left '*' '/' '%'
%nonassoc '?'
%nonassoc tUNARY

%type <node> declaration variable function procedure instruction condition elifelse iteration
%type <sequence> exprs file declarations variables blockdecls instructions
%type <t> type
%type <b> block
%type <s> string
%type <i> qualifier
%type <expression> expr integer real
%type <lvalue> lval

%{
//-- The rules below will be included in yyparse, the main parsing function.
%}
%%

file           :                                  { compiler->ast($$ = new cdk::sequence_node(LINE)); }
               | declarations                     { compiler->ast($$ = $1); }
               ;

declarations   : declaration                      { $$ = new cdk::sequence_node(LINE,$1); }
               | declarations declaration         { $$ = new cdk::sequence_node(LINE,$2,$1); }
               ;

declaration    : variable ';'                     { $$ = $1; }
               | function                         { $$ = $1; }
               | procedure                        { $$ = $1; }
               ;

variable       : qualifier type tIDENTIFIER            { $$ = new og::variable_declaration_node(LINE,$1,std::shared_ptr<cdk::basic_type>($2),*$3,nullptr); }
               | qualifier type tIDENTIFIER '=' expr   { $$ = new og::variable_declaration_node(LINE,$1,std::shared_ptr<cdk::basic_type>($2),*$3,$5); }
               ;

function       : qualifier type tIDENTIFIER '(' ')'                   { $$ = new og::function_declaration_node(LINE,$1,std::shared_ptr<cdk::basic_type>($2),*$3,nullptr); }   
               | qualifier type tIDENTIFIER '(' variables ')'         { $$ = new og::function_declaration_node(LINE,$1,std::shared_ptr<cdk::basic_type>($2),*$3,$5); }
               | qualifier type tIDENTIFIER '('  ')' block            { $$ = new og::function_definition_node(LINE,$1,std::shared_ptr<cdk::basic_type>($2),*$3,nullptr,$6); }
               | qualifier type tIDENTIFIER '(' variables ')' block   { $$ = new og::function_definition_node(LINE,$1,std::shared_ptr<cdk::basic_type>($2),*$3,$5,$7); }
               ;


procedure      : qualifier tPROCEDURE tIDENTIFIER '(' ')'                   { $$ = new og::function_declaration_node(LINE,$1,nullptr,*$3,nullptr); }   
               | qualifier tPROCEDURE tIDENTIFIER '(' variables ')'         { $$ = new og::function_declaration_node(LINE,$1,nullptr,*$3,$5); }
               | qualifier tPROCEDURE tIDENTIFIER '('  ')' block            { $$ = new og::function_definition_node(LINE,$1,nullptr,*$3,nullptr,$6); }
               | qualifier tPROCEDURE tIDENTIFIER '(' variables ')' block   { $$ = new og::function_definition_node(LINE,$1,nullptr,*$3,$5,$7); }
               ;


exprs          : expr                             {  $$ = new cdk::sequence_node(LINE, $1); }
               | exprs ',' expr                   {  $$ = new cdk::sequence_node(LINE, $3, $1); }
               ;

expr           : integer                                    {  $$ = $1; }
               | string                                     {  $$ = new cdk::string_node(LINE, $1); }
               | real                                       {  $$ = $1; }
               | tINPUT					    { $$ = new og::read_node(LINE); }
               | tNULLPTR				    { $$ = new og::nullptr_node(LINE); }
	       | tIDENTIFIER '(' ')'			                    {$$ = new og::function_call_node(LINE,*$1, nullptr); delete $1; }
      	       | tIDENTIFIER '(' exprs ')'		            {$$ = new og::function_call_node(LINE,*$1, $3); delete $1; }
               | '-' expr %prec tUNARY                      {  $$ = new cdk::neg_node(LINE, $2); }
               | '+' expr %prec tUNARY 		            { $$ = new og::identity_node(LINE,$2);}
               | expr '+' expr	                            {  $$ = new cdk::add_node(LINE, $1, $3); }
               | expr '-' expr	                            {  $$ = new cdk::sub_node(LINE, $1, $3); }
               | expr '*' expr	                            {  $$ = new cdk::mul_node(LINE, $1, $3); }
               | expr '/' expr	                            {  $$ = new cdk::div_node(LINE, $1, $3); }
               | expr '%' expr	                            {  $$ = new cdk::mod_node(LINE, $1, $3); }
               | expr '<' expr	                            {  $$ = new cdk::lt_node(LINE, $1, $3); }
               | expr '>' expr	                            {  $$ = new cdk::gt_node(LINE, $1, $3); }
               | expr tGE expr	                            {  $$ = new cdk::ge_node(LINE, $1, $3); }
               | expr tLE expr                              {  $$ = new cdk::le_node(LINE, $1, $3); }
               | expr tNE expr	                            {  $$ = new cdk::ne_node(LINE, $1, $3); }
               | expr tEQ expr	                            {  $$ = new cdk::eq_node(LINE, $1, $3); }
               | '(' expr ')'                               {  $$ = $2; }
               | lval                                       {  $$ = new cdk::rvalue_node(LINE, $1); }  //FIXME
               | lval '=' expr                              {  $$ = new cdk::assignment_node(LINE, $1, $3); }
| expr tAND expr				            { $$ = new cdk::and_node(LINE, $1,$3); }
               | expr tOR expr				    { $$ = new cdk::or_node(LINE, $1,$3); }
               | lval '?' 				    { $$ = new og::address_node(LINE,$1); }
               |'[' expr ']'				    { $$ = new og::stack_alloc_node(LINE,$2); } //FIXME
               | tSIZEOF '(' expr ')'  		            { $$ = new og::sizeof_node(LINE,$3); }
               ;

variables      : variable                         { $$ = new cdk::sequence_node(LINE,$1); }
               | variables ',' variable           { $$ = new cdk::sequence_node(LINE,$3,$1); }
               ;

type           : tINT                             { $$ = new cdk::primitive_type(4,cdk::TYPE_INT); }
               | tREAL                            { $$ = new cdk::primitive_type(8,cdk::TYPE_DOUBLE); }
               | tSTR                             { $$ = new cdk::primitive_type(4,cdk::TYPE_STRING); }
               | tAUTO                            { $$ = new cdk::primitive_type(0,cdk::TYPE_UNSPEC); }
               | tPTR '<' type '>'                { $$ = new cdk::reference_type(4,std::shared_ptr<cdk::basic_type>($3));}
               ;


qualifier      :              { $$ = tPRIVATE; }
               | tPUBLIC      { $$ = tPUBLIC; }
               | tREQUIRE     { $$ = tREQUIRE; }
               ;

block          : '{' '}'                                { $$ = new og::block_node(LINE,nullptr,nullptr); }
               | '{' blockdecls '}'                     { $$ = new og::block_node(LINE,$2,nullptr); }
               | '{' instructions '}'                   { $$ = new og::block_node(LINE,nullptr,$2); }
               | '{' blockdecls instructions '}'        { $$ = new og::block_node(LINE,$2,$3); }
               ;

blockdecls      : variable ';'            { $$ = new cdk::sequence_node(LINE, $1);     }
                | blockdecls variable ';' { $$ = new cdk::sequence_node(LINE, $2, $1); }
                ;

instructions   : instruction                           { $$ = new cdk::sequence_node(LINE, $1); }
               | instructions instruction              { $$ = new cdk::sequence_node(LINE, $2, $1); }
               ;

instruction    : expr ';'               { $$ = new og::evaluation_node(LINE,$1); }
               | tWRITE exprs ';'       { $$ = new og::print_node(LINE,$2,false); }
               | tWRITELN exprs ';'     { $$ = new og::print_node(LINE,$2,true); }
               | tBREAK                 { $$ = new og::break_node(LINE); }
               | tCONTINUE              { $$ = new og::continue_node(LINE); }
               | tRETURN ';'            { $$ = new og::return_node(LINE,nullptr); }
               | tRETURN exprs ';'      { $$ = new og::return_node(LINE,$2); }
               | condition              { $$ = $1;}
               | iteration              { $$ = $1; }
               | block                  { $$ = $1; }
               ;

condition      : tIF expr tTHEN instruction                           { $$ = new og::if_node(LINE,$2,$4); }
               | tIF expr tTHEN instruction elifelse                  { $$ = new og::if_else_node(LINE,$2,$4,$5); }
               ;

elifelse       : tELSE instruction                                   { $$ = $2; }
               | tELIF expr tTHEN instruction                        { $$ = new og::if_node(LINE,$2,$4); }
               | tELIF expr tTHEN instruction elifelse               { $$ = new og::if_else_node(LINE,$2,$4,$5); }
               ;


iteration      : tFOR ';' ';'  tDO instruction                        { $$ = new og::for_node(LINE,nullptr,nullptr,nullptr,$5); }
               | tFOR variables ';' ';' tDO instruction               { $$ = new og::for_node(LINE,$2,nullptr,nullptr,$6); }
               | tFOR ';' exprs ';' tDO instruction                   { $$ = new og::for_node(LINE,nullptr,$3,nullptr,$6); }
               | tFOR ';' ';' exprs tDO instruction                   { $$ = new og::for_node(LINE,nullptr,nullptr,$4,$6); }
               | tFOR variables ';' exprs ';' tDO instruction         { $$ = new og::for_node(LINE,$2,$4,nullptr,$7); }
               | tFOR variables ';' ';' exprs tDO instruction         { $$ = new og::for_node(LINE,$2,nullptr,$5,$7); }
               | tFOR ';' exprs ';' exprs tDO instruction             { $$ = new og::for_node(LINE,nullptr,$3,$5,$7); }
               | tFOR variables ';' exprs ';' exprs tDO instruction   { $$ = new og::for_node(LINE,$2,$4,$6,$8); }
               | tFOR exprs ';' ';' tDO instruction                   { $$ = new og::for_node(LINE,$2,nullptr,nullptr,$6); }
               | tFOR exprs ';' exprs ';' tDO instruction             { $$ = new og::for_node(LINE,$2,$4,nullptr,$7); }
               | tFOR exprs ';' ';' exprs tDO instruction             { $$ = new og::for_node(LINE,$2,nullptr,$5,$7); }               
               | tFOR exprs ';' exprs  ';' exprs tDO instruction      { $$ = new og::for_node(LINE,$2,$4,$6,$8); }
               ;


lval    : tIDENTIFIER                               { $$ = new cdk::variable_node(LINE, *$1); }
	| lval '[' expr ']' 	                    { $$ = new og::left_index_node(LINE, new cdk::rvalue_node(LINE,$1), $3);}
	| '(' expr ')' '[' expr ']'                 { $$ = new og::left_index_node(LINE, $2, $5); }
	| tIDENTIFIER '(' ')' '[' expr ']' 	    { $$ = new og::left_index_node(LINE, new og::function_call_node(LINE,*$1, nullptr), $5); delete $1;}
        | tIDENTIFIER '(' exprs ')' '[' expr ']'    { $$ = new og::left_index_node(LINE, new og::function_call_node(LINE,*$1, $3), $6); delete $1;}
        ;

integer         : tINTEGER                      { $$ = new cdk::integer_node(LINE, $1); };
real            : tREALVAL                      { $$ = new cdk::double_node(LINE, $1); };
string          : tSTRING                       { $$ = $1; }
                | string tSTRING                { $$ = new std::string(*$1 + *$2); delete $1; delete $2; }
                ;

%%
